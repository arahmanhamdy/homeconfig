#! /bin/zsh
#
# vi-mode colour changing
#   http://www.zsh.org/mla/users/2006/msg01196.html

setopt prompt_subst
autoload colors
autoload vcs_info
colors

rst="%{%b%s%u$reset_color%}"
bgc="%{%(?.$rst.%S)%}"
zstyle ':vcs_info:*' enable git hg svn
zstyle ':vcs_info:*' check-for-changes true
zstyle ':vcs_info:*' stagedstr  'S '
zstyle ':vcs_info:*' unstagedstr  'U '
zstyle ':vcs_info:*' actionformats '%F{5}[%F{2}%c%u%b%F{3}|%F{1}%a%F{5}]%f ' '%R'
zstyle ':vcs_info:*' formats       '%F{5}[%F{2}%c%u%b%F{5}]%f ' '%R'
zstyle ':vcs_info:(sv[nk]|bzr):*' branchformat '%b%F{1}:%F{3}%r'
precmd_functions+='vcs_info'

function lprompt {
    local col1 col2 ch1 ch2
    col1="%{%b$fg[$2]%}"
    col2="%{$4$fg[$3]%}"
    ch1=$col1${1[1]}
    ch2=$col1${1[2]}

    PROMPT1="\
$bgc$ch1\
$bgc$col2%B%1~%b\
$ch2$rst \
$col2%#$rst "
    PROMPT='${vcs_info_msg_0_}$PROMPT1'
}

function _rprompt_dir {
    local col_p rest lead
    col_p="%{$fg[yellow]%}"

    local short
    short="${PWD/$HOME/~}"

    if test -n "${vcs_info_msg_1_}" ; then
        lead="${vcs_info_msg_1_/$HOME/~}"
    else
        echo -n "$short"
        return
    fi

    rest=${${short#$lead}#/}

    echo -n "$lead$col_p/$rest"
}

function rprompt {
    local col1 col2 ch1 ch2
    col1="%{$fg[$2]%}"
    col2="%{$4$fg[$3]%}"
    ch1=$col1${1[1]}
    ch2=$col1${1[2]}

    local _dir='$(_rprompt_dir)'

    RPROMPT="\
$rst$ch1\
$col2%n@%m\
$col1:\
$col2%B$_dir%b\
$ch2$rst"
}

if [ $UID -eq 0 ]; then
    lprompt '[]' yellow red
    rprompt '()' red red
elif [ -n "$SSH_CLIENT" ]; then
    lprompt '<>' yellow blue
    rprompt '<>' red white
else
    lprompt '[]' bold green
    rprompt '()' yellow white
fi

if [ -n "$debian_chroot" ]; then
    PROMPT="$bgc%{$fg[yellow]%}%B${debian_chroot}%b ${PROMPT}"
fi

if [ "$TERM" = "screen" -o -n "$WINDOW" ]; then
    PROMPT="${PROMPT}%{kzsh\\%}"

    preexec () {
        local CMD=${1[(wr)^(*=*|sudo|-*)]}
        echo -ne "\ek$CMD\e\\"
        update_display
    }
fi

unset rst bgc

if [[ "$TERM" != "linux" ]]; then

  # this updates title bar before prompt is printed
  precmd () {
    echo  -n \\033\]2\;$USER@$HOST:$_\\07;
  }

  # this updates title bar while command is running
  xterm-accept-line () { print -nr -- "]2;$BUFFER"; zle .accept-line; }
  zle -N accept-line xterm-accept-line

fi #TERM


# ------------------------
#       PS1=$'%{\e[34;1m%}%20>..>%1~%>>>%{\e[0m%}'
#
#       function zle-keymap-select {
#               PS1=${${1/vicmd/$'%{\e[31;1m%}'}/(viins|main)/$'%{\e[34;1m%}'}${PS1#*%\}}
#               zle reset-prompt
#       }


# ------------------------
#       function zle-line-init zle-keymap-select {
#               RPS1="${${KEYMAP/vicmd/-- NORMAL --}/(main|viins)/-- INSERT --}"
#               RPS2=$RPS1
#               RPS3=$RPS1
#               RPS4=$RPS1
#               zle reset-prompt
#       }
#       zle -N zle-line-init
